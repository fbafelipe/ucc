<EXPRESSION> ::= <CONDITIONAL_EXPRESSION>;

<PRIMARY_EXPRESSION> ::= CONSTANT
		| P_OPEN <EXPRESSION> P_CLOSE
		| DEFINED P_OPEN IDENTIFIER P_CLOSE
		| DEFINED_M IDENTIFIER P_CLOSE
		| DEFINED IDENTIFIER
		| IDENTIFIER
		;

<UNARY_EXPRESSION> ::= <PRIMARY_EXPRESSION>
		| <UNARY_OPERATOR> <PRIMARY_EXPRESSION>
		;

<UNARY_OPERATOR> ::= PLUS_SIG
		| LESS_SIG
		| NEG
		| NOT
		;

<MULTIPLICATIVE_EXPRESSION> ::= <UNARY_EXPRESSION>
		| <MULTIPLICATIVE_EXPRESSION> MUL <UNARY_EXPRESSION>
		| <MULTIPLICATIVE_EXPRESSION> DIV <UNARY_EXPRESSION>
		| <MULTIPLICATIVE_EXPRESSION> MOD <UNARY_EXPRESSION>
		;

<ADDITIVE_EXPRESSION> ::= <MULTIPLICATIVE_EXPRESSION>
		| <ADDITIVE_EXPRESSION> PLUS_SIG <MULTIPLICATIVE_EXPRESSION>
		| <ADDITIVE_EXPRESSION> LESS_SIG <MULTIPLICATIVE_EXPRESSION>
		;

<SHIFT_EXPRESSION> ::= <ADDITIVE_EXPRESSION>
		| <SHIFT_EXPRESSION> LEFT_OP <ADDITIVE_EXPRESSION>
		| <SHIFT_EXPRESSION> RIGHT_OP <ADDITIVE_EXPRESSION>
		;

<RELATIONAL_EXPRESSION> ::= <SHIFT_EXPRESSION>
		| <RELATIONAL_EXPRESSION> LESS <SHIFT_EXPRESSION>
		| <RELATIONAL_EXPRESSION> GREATER <SHIFT_EXPRESSION>
		| <RELATIONAL_EXPRESSION> LE_OP <SHIFT_EXPRESSION>
		| <RELATIONAL_EXPRESSION> GE_OP <SHIFT_EXPRESSION>
		;

<EQUALITY_EXPRESSION> ::= <RELATIONAL_EXPRESSION>
		| <EQUALITY_EXPRESSION> EQ_OP <RELATIONAL_EXPRESSION>
		| <EQUALITY_EXPRESSION> NE_OP <RELATIONAL_EXPRESSION>
		;

<AND_EXPRESSION> ::= <EQUALITY_EXPRESSION>
		| <AND_EXPRESSION> AND <EQUALITY_EXPRESSION>
		;

<EXCLUSIVE_OR_EXPRESSION> ::= <AND_EXPRESSION>
		| <EXCLUSIVE_OR_EXPRESSION> XOR <AND_EXPRESSION>
		;

<INCLUSIVE_OR_EXPRESSION> ::= <EXCLUSIVE_OR_EXPRESSION>
		| <INCLUSIVE_OR_EXPRESSION> OR <EXCLUSIVE_OR_EXPRESSION>
		;

<LOGICAL_AND_EXPRESSION> ::= <INCLUSIVE_OR_EXPRESSION>
		| <LOGICAL_AND_EXPRESSION> AND_OP <INCLUSIVE_OR_EXPRESSION>
		;

<LOGICAL_OR_EXPRESSION> ::= <LOGICAL_AND_EXPRESSION>
		| <LOGICAL_OR_EXPRESSION> OR_OP <LOGICAL_AND_EXPRESSION>
		;

<CONDITIONAL_EXPRESSION> ::= <LOGICAL_OR_EXPRESSION>
		| <LOGICAL_OR_EXPRESSION> QUESTION <EXPRESSION> COLUMN <CONDITIONAL_EXPRESSION>
		;
